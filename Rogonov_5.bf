Ввод: a b c n m
++ > ++ > ++ > ++ > ++

Оставляем место для:
    @ переменных x y z s
    @ счётчика цикла i
    @ локального в контексте i x
    @ счётчика цикла j
    @ локального в контексте j x
>>>>>>>>


Начало выполнения программы
Сдвиг в начало рабочей области
> 

1) x = 2 * (c plus b)

Копируем с восстановлением переменную "c"
<<<<<<<<<<< [->>>>>>>>>>>+>+<<<<<<<<<<<<] 
>>>>>>>>>>> [-<<<<<<<<<<<+>>>>>>>>>>>]

Скопированное значение "c" прибавляем к "x"
> [-<<<<<<<<<+>>>>>>>>>]

Копируем с восстановлением переменную "b"
<<<<<<<<<<<<< [->>>>>>>>>>>>+>+<<<<<<<<<<<<<]
>>>>>>>>>>>> [-<<<<<<<<<<<<+>>>>>>>>>>>>]

Скопированное значение "b" прибавляем к "x"
> [-<<<<<<<<<+>>>>>>>>>] <

Сумму "c" и "b" из ячейки "x" перекладываем в ячейку 1
<<<<<<<< [->>>>>>>>>+<<<<<<<<<] >>>>>>>

В ячейку 2 загружаем "2"
> ++

В ячейке 1 лежит сумма "c" и "b"
В ячейке 2 лежит "2"

Производим умножение ячеек 1 и 2
[-> [->+>+<<] > [-<+>] <<]

Во 2 ячейке лежит множитель
В 4 ячейке лежит результат

Очищаем множитель за ненадобностью
> [-]

Результат умножения перекладываем в ячейку "x"
>> [-<<<<<<<<<<<+>>>>>>>>>>>]

Сдвигаемся в начало рабочей области
<<< 

2) y = (6 plus a) * (x minus c)

Копируем с восстановлением "a"
<<<<<<<<<<<<< [->>>>>>>>>>>>>+>+<<<<<<<<<<<<<<]
Здесь восстановление значения "a" производится из ячейки 1
>>>>>>>>>>>>> [-<<<<<<<<<<<<<+>>>>>>>>>>>>>]

В ячейку 1 загружаем "6" и складываем её с "a"
++++++ > [-<+>]

В ячейке 1 лежит результат суммы "6" и "a"

Копируем с восстановлением "x"
<<<<<<<<< [->>>>>>>>>+>+<<<<<<<<<<]
>>>>>>>>>> [-<<<<<<<<<<+>>>>>>>>>>] 

Копируем с восстановлением "c"
<<<<<<<<<<<<< [->>>>>>>>>>>>>+>+<<<<<<<<<<<<<<]
>>>>>>>>>>>>>> [-<<<<<<<<<<<<<<+>>>>>>>>>>>>>>]

В ячейке 2 лежит "x"
В ячейке 3 лежит "c"
Головка указывает на ячейку 4

Сдвигаем головку на "c"
И вычитаем "c" из "x"
< [-<->]

В ячейке 1 лежит результат (6 plus a)
В ячейке 2 лежит результат (x minus c)

Возващаем головку на ячейку 1 и
Умножаем эти два результата
<< [-> [->+>+<<] > [-<+>] <<] 

Очищаем множитель за ненадобностью
> [-] 

Передвигаем головку на результат умножения
и переносим его в ячейку "y"
>> [-<<<<<<<<<<+>>>>>>>>>>] 

Далее примитив For Loop по счётчику i
<< +
[
    -

    <<<<< [->>>>>+>+<<<<<<]
    >>>>>> [-<<<<<<+>>>>>>] 

    <<<<<<<<<<<< [->>>>>>>>>>>>+>+<<<<<<<<<<<<<]
    >>>>>>>>>>>>> [-<<<<<<<<<<<<<+>>>>>>>>>>>>>]

    < [-<->] <

    [
        [+]

        Начало части тела цикла For Loop по счётчику i

        3) x = c minus 3

        Копирование с восстановлением "c"
        <<<<<<<<<<<< [->>>>>>>>>>>>+>+<<<<<<<<<<<<<]
        >>>>>>>>>>>>> [-<<<<<<<<<<<<<+>>>>>>>>>>>>>]

        В ячейку 3 загружаем "3"
        И вычитаем её из ячейки 2
        После чего сдвигаем головку на ячейку 2
        которая хранит результат разности
        +++ [-<->] <

        Данный результат мы кладём в локальный в контексте i "x" за три действия:
        
        Очищаем локальный в контексте i "х"
        Возвращаемся к ячейке 2 хранящей его новое значение
        Перекладываем новое значение в локальный в контексте i "х"
        <<<< [-] >>>> [-<<<<+>>>>]

        4) y = a plus c

        Копирование с восстановлением "a"
        <<<<<<<<<<<<<< [->>>>>>>>>>>>>+>+<<<<<<<<<<<<<<]
        >>>>>>>>>>>>>> [-<<<<<<<<<<<<<<+>>>>>>>>>>>>>>] 

        Копирование с восстановлением "c"
        <<<<<<<<<<<< [->>>>>>>>>>>>+>+<<<<<<<<<<<<<]
        >>>>>>>>>>>>> [-<<<<<<<<<<<<<+>>>>>>>>>>>>>]

        В ячейке 1 лежит "a"
        В ячейке 2 лежит "c"

        Сдвиг на ячейку 2
        Складываем "a" и "c"
        < [-<+>]

        В ячейке 1 хранится результат суммы "a" и "c"

        Данный результат мы кладём в "y" за три действия:

        Сдвигаемся к "y" и обнуляем его
        Возвращаемся к ячейке 1
        Перекладываем новое значение в "y"
        <<<<<<<< [-] >>>>>>> [-<<<<<<<+>>>>>>>] 
        
        Возвращемся в рабочую область
        >

        5) z = (a plus x) * (y minus 8)

        Копирование с восстановлением "a"
        <<<<<<<<<<<<<< [->>>>>>>>>>>>>>+>+<<<<<<<<<<<<<<<]
        >>>>>>>>>>>>>>> [-<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>]

        Копирование с восстановлением локального в контексте i "x"
        <<<<< [->>>>>+>+<<<<<<] 
        >>>>>> [-<<<<<<+>>>>>>] 

        В ячейке 2 лежит "a"
        В ячейке 3 лежит локальный в контексте i "x"

        Сдвиг головки в ячейку 3
        Суммируем "а" и "х"
        < [-<+>]

        В ячейке 2 лежит результат суммы "a" и "x"
        Головка указывает на ячейку 3

        Копирование с восстановлением "y"
        <<<<<<<<< [->>>>>>>>>+>+<<<<<<<<<<]
        >>>>>>>>>> [-<<<<<<<<<<+>>>>>>>>>>]

        В ячейку 4 кладём "8"
        ++++ ++++

        В ячейке 3 лежит "y"
        В ячейке 4 лежит "8"
        Головка указывает на ячейку 4

        Вычитаем "8" из "y"
        [-<->]

        Сдвигаем головку на ячейку 2
        <<

        В ячейке 2 лежит сумма "a" и "x"
        В ячейке 3 лежит разность "y" и "8'
        Головка указывает на ячейку 2

        Производим умножение ячеек 2 и 3
        [-> [->+>+<<] > [-<+>] <<]

        Очищаем множитель за ненадобностью
        > [-]

        Сдвигаемся к результату умножения
        >>

        Результат произведения перекладываем в ячейку 2
        [-<<<+>>>]

        Сдвигаем головку к ячейке 2
        <<<

        Данный результат мы кладём в "z" за три действия:
        
        Очищаем "z"
        Возвращаемся к ячейке 2 хранящей его новое значение
        Перекладываем новое значение в "z"
        <<<<<<< [-] >>>>>>> [-<<<<<<<+>>>>>>>]

        Далее примитив For Loop по счётчику j

        >> +

        [
            - 
            <<<<< [->>>>>+>+<<<<<<] 
            >>>>>> [-<<<<<<+>>>>>>] 

            <<<<<<<<<<<<< [->>>>>>>>>>>>>+>+<<<<<<<<<<<<<<] 
            >>>>>>>>>>>>>> [-<<<<<<<<<<<<<<+>>>>>>>>>>>>>>]

            < [-<->] <

            [
                [+]

                Начало части тела цикла For Loop по счётчику j

                6) x = (z plus b) * (x plus c)

                Копирование с восстановлением "z"
                <<<<<<<<< [->>>>>>>>>+>+<<<<<<<<<<] 
                >>>>>>>>>> [-<<<<<<<<<<+>>>>>>>>>>]

                Копирование с восстановлением "b"
                <<<<<<<<<<<<<<<< [->>>>>>>>>>>>>>>>+>+<<<<<<<<<<<<<<<<<] 
                >>>>>>>>>>>>>>>>> [-<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>]

                В ячейке 4 лежит "z"
                В ячейке 5 лежит "b"
                Головка указывает на ячейку 6

                Передвигаем головку на ячейку 5
                И складываем ячейки 4 и 5
                < [-<+>]

                Копирование с восстановлением локального в контексте i "x"
                <<<<<<< [->>>>>>>+>+<<<<<<<<] 
                >>>>>>>> [-<<<<<<<<+>>>>>>>>]

                Копирование с восстановлением "c"
                <<<<<<<<<<<<<<<< [->>>>>>>>>>>>>>>>+>+<<<<<<<<<<<<<<<<<] 
                >>>>>>>>>>>>>>>>> [-<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>]

                
                В ячейке 5 лежит "x"
                В ячейке 6 лежит "c"
                Головка указывает на ячейку 7

                Передвигаем головку на ячейку 6
                И складываем ячейки 5 и 6
                < [-<+>]
                
                В ячейке 4 лежит сумма "z" и "b"
                В ячейке 5 лежит сумма "x" и "c"
                Головка указывает на ячейку 6
                
                Сдвигаем головку на ячейку 4
                Где лежит первый множитель
                <<

                Умножаем сумму на сумму
                [-> [->+>+<<] > [-<+>] <<]

                Очищаем множитель за ненадобностью
                > [-] 
                
                Передвигаем результат умножения в ячейку 4
                >> [-<<<+>>>] <<<

                Данный результат мы кладём в локальный в контексте j "x" за три действия:

                Сдвигаемся к "x" и обнуляем его
                Возвращаемся к ячейке 4
                Перекладываем новое значение в "x"

                <<<< [-] >>>> [-<<<<+>>>>]

                7) s = s plus b plus c minus x

                Перенос "s" в ячейку 4
                и сдвиг головки в ячейку 5
                <<<<<<<< [->>>>>>>>+<<<<<<<<] >>>>>>>>>

                Копирование с восстановлением "b"
                <<<<<<<<<<<<<<<< [->>>>>>>>>>>>>>>>+>+<<<<<<<<<<<<<<<<<] 
                >>>>>>>>>>>>>>>>> [-<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>]

                В ячейке 4 лежит "s"
                В ячейке 5 лежит "b"
                Головка указывает на ячейку 6

                Сдвигаем головку в ячейку 5 и складываем "s" с "b"
                < [-<+>]

                Копирование с восстановлением "c"
                <<<<<<<<<<<<<<< [->>>>>>>>>>>>>>>+>+<<<<<<<<<<<<<<<<] 
                >>>>>>>>>>>>>>>> [-<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>]

                В ячейке 4 лежит cумма "s" и "b"
                В ячейке 5 лежит "c"
                Головка указывает на ячейку 6

                Сдвигаем головку в ячейку 5 и складываем 4 с "c"
                < [-<+>]

                Копирование с восстановлением локального в контексте j "x"
                <<<<< [->>>>>+>+<<<<<<] 
                >>>>>> [-<<<<<<+>>>>>>]

                В ячейке 4 лежит cумма "s" и "b" и "c"
                В ячейке 5 лежит "x"
                Головка указывает на ячейку 6

                Сдвигаем головку в ячейку 5 и вычитаем из 4 "x"
                < [-<->] < 
                
                Перекладываем результат в "s"
                [-<<<<<<<<+>>>>>>>>]
                

                Продолжение примитива For Loop по счётчику i
                <<<<< + >>>>>

                + >
            ]
            
            <
        ]
        <
        
        Очищаем счётчик j
        <<< [-] >>>

        Очищаем локальный в контексте j "x"
        << [-] >>

        Продолжение примитива For Loop по счётчику i
        <<<<< + >>>>>
        + >
    ]

    Очищаем локальный в контексте i "x"
    <<<<< [-] >>>>>

    <
]

Сдвиг головки в ячейку "a"
Она же ячейка 0 в общей нумерации ячеек
<<<<<<<<<<<<<

Код закончил работу! Your brain f*cked!
